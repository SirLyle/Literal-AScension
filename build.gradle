buildscript {
    ext.kotlin_version = '1.0.6'
    ext.corePlugin = 'com.jamieswhiteshirt.literalascension.core.plugin.LiteralAscensionPlugin'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.7'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'idea'

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

version = "1.10.2-1.0.2.2"
group= "com.jamieswhiteshirt.literalascension"
archivesBaseName = "literalascension"

minecraft {
    version = "1.10.2-12.18.2.2099"
    runDir = "run"

    clientJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
    serverJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20160518"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven {
        name 'mcmoddev'
        url 'http://maven.mcmoddev.com'
    }
    maven {
        name 'shadowfacts'
        url 'http://mvn.rx14.co.uk/shadowfacts/'
    }
}

dependencies {
    compile "net.ilexiconn:llibrary:1.7.2-1.10.2:dev"
    compile group: "net.shadowfacts", name: "Forgelin", version: '1.3.0'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    main.kotlin.srcDirs += 'src/main/kotlin'
}

idea {
    module {
        inheritOutputDirs = true
    }
    project {
        languageLevel = '1.8'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes "FMLCorePlugin": corePlugin
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes "FMLCorePlugin": corePlugin
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

curseforge {
	apiKey = project.curseForgeApiKey
	project {
		id = '253021'
		changelogType = 'markdown'
		changelog = file('changelog.md')
		releaseType = 'release'
		relations {
			requiredLibrary 'shadowfacts-forgelin'
			requiredLibrary 'llibrary'
            optionalLibrary 'chisel'
            optionalLibrary 'environmental-tech'
            optionalLibrary 'biomes-o-plenty'
		}
		mainArtifact(jar) {
			displayName = "Literal Ascension $project.version"
		}
		addArtifact(sourcesJar) {
			displayName = "Literal Ascension $project.version Sources"
		}
		addArtifact(deobfJar) {
			displayName = "Literal Ascension $project.version Deobfuscated"
		}
	}
}

// AUTOMATIC MODEL GENERATION

ext.args = [:]

ext.requireArgument = {String property, String displayValue ->
    def value = args[property]
    if (value == null) {
        throw new InvalidUserDataException("$property must be set with \'$property=$displayValue\'")
    }
    return value
}

tasks.addRule("Pattern: <property>=<value>: Passes arguments to the scripts") { String taskName ->
    def match = taskName =~ /(.*?)=(.*?$)/
    if (match) {
        def property = match[0][1]
        def value = match[0][2]
        ext.args[property] = value;
        task(taskName) << {
            println "Passes value \'$value\' to args[\'$property\']"
        }
    }
}

import org.apache.tools.ant.filters.FixCrLfFilter

class CopyJsonTemplate extends DefaultTask {
    @Input
    def template

    @Input
    def destination = 'src/main/resources'

    @Input
    def arguments

    @Input
    def jsonRename

    @TaskAction
    def build() {
        def args = arguments()
        project.copy {
            from("templates/${template}") {
                expand(args)
                filter(FixCrLfFilter, eol: FixCrLfFilter.CrLf.newInstance('crlf'))
            }
            into destination
            rename { String filename ->
                def match = filename =~ /(.*)\.json/
                if (match) {
                    def prevFilename = match[0][1]
                    def newFilename = jsonRename.call(prevFilename)
                    return "${newFilename}.json"
                }
                else {
                    return filename
                }
            }
        }
    }
}


task generateBlockModel(type: CopyJsonTemplate) {
    template 'block'
    arguments {
        def blockName = requireArgument('blockName', 'block_name')
        return ['modid':archivesBaseName, 'block_name':blockName]
    }
    jsonRename {
        return args['blockName']
    }
}

task generateItemModel(type: CopyJsonTemplate) {
    template 'item'
    arguments {
        def itemName = requireArgument('itemName', 'item_name')
        return ['modid':archivesBaseName, 'item_name':itemName]
    }
    jsonRename {
        return args['itemName']
    }
}

task generateStepladderModel(type: CopyJsonTemplate) {
    template 'stepladder'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        def feet = requireArgument('feet', 'feet')
        def steps = requireArgument('steps', 'steps')
        return ['material':material, 'domain':domain, 'feet':feet, 'steps':steps]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateBOPStepladderModel(type: CopyJsonTemplate) {
    template 'bop_stepladder'
    arguments {
        def material = requireArgument('material', 'material')
        return ['material':material]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateChuteStoneModel(type: CopyJsonTemplate) {
    template 'chute_stone'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateChuteLogModel(type: CopyJsonTemplate) {
    template 'chute_log'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateChuteVariantsModel(type: CopyJsonTemplate) {
    template 'chute_variants'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateNotchedLogModel(type: CopyJsonTemplate) {
    template 'notched_log'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateNotchedVariantsModel(type: CopyJsonTemplate) {
    template 'notched_variants'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateNotchedStoneModel(type: CopyJsonTemplate) {
    template 'notched_stone'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return args['material'] + prevName
    }
}

task generateChiselCarvableRaw(type: CopyJsonTemplate) {
    template 'chisel_carvable_raw'
    arguments {
        def material = requireArgument('material', 'material')
        def domain = requireArgument('domain', 'domain')
        return ['material':material, 'domain':domain]
    }
    jsonRename { String prevName ->
        return "${args['material']}_raw${prevName}"
    }
}
